plugins {
    id 'java'
    id 'com.matyrobbrt.mc.registrationutils' version '0.2.6'
}

registrationUtils {
    group 'me.hypherionmc.craterlib.systems.reg'
    projects {
        Common { type 'common'; project ':Common' }
        Fabric { type 'fabric'; project ':Fabric' }
        Forge { type 'forge'; project ':Forge' }
    }
}

subprojects {

    ext {
        isSnapshot: false
    }

    def version_base = "${project.version_major}.${project.version_minor}"
    version = "${version_base}.${project.version_patch}"

    group = project.group

    // Jenkins
    if (System.getenv('BUILD_NUMBER') != null) {
        project.isSnapshot = true
        version = version_base + "." + System.getenv('BUILD_NUMBER') + "d"
    }

    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    //java.withSourcesJar()
    //java.withJavadocJar()

    jar {
        manifest {
            attributes([
                    'Specification-Title'     : mod_name,
                    'Specification-Vendor'    : mod_author,
                    'Specification-Version'   : project.jar.archiveVersion,
                    'Implementation-Title'    : project.name,
                    'Implementation-Version'  : project.jar.archiveVersion,
                    'Implementation-Vendor'   : mod_author,
                    'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    'Timestampe'              : System.currentTimeMillis(),
                    'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                    'Build-On-Minecraft'      : minecraft_version
            ])
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'Hypherion Maven'
            url = 'https://maven.firstdarkdev.xyz/releases/'
        }
    }

    dependencies {
        implementation 'me.hypherionmc.night-config:toml:3.6.5_custom'
        implementation 'me.hypherionmc.night-config:core:3.6.5_custom'
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 17
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    clean {
        delete "$rootDir/artifacts"
    }
}
