plugins {
    id 'java'
    id 'fabric-loom' version '0.12-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

archivesBaseName = "${mod_name}-common-${minecraft_version}"

loom {
    shareCaches()
    accessWidenerPath = project.file("src/main/resources/craterlib.aw")
    remapArchives = false
    mixin {
        useLegacyMixinAp = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    implementation "com.hypherionmc:rpcsdk:1.0"
}

processResources {
    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {
        expand buildProps
    }
}

shadowJar {
    dependencies {
        include(dependency("me.hypherionmc.moon-config:core:${moon_config}"))
        include(dependency("me.hypherionmc.moon-config:toml:${moon_config}"))

        relocate 'me.hypherionmc.moonconfig', 'shadow.hypherionmc.moonconfig'
    }
    classifier ''
}

build.dependsOn shadowJar
reg.configureJarTask(shadowJar)

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            artifact(jar) {
                builtBy jar
            }

            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'regutils-joined-fabric' ||
                            it.artifactId.text() == 'core' ||
                            it.artifactId.text() == 'toml'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }
    }

    repositories {
        maven {
            url "https://maven.firstdark.dev/" + (project.release ? "releases" : "snapshots")
            credentials {
                username System.getenv("MAVEN_USER")
                password System.getenv("MAVEN_PASS")
            }
        }
    }
}
